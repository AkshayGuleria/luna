import Std.Base

class HttpMethod:
    POST
    GET
    PUT
    DELETE

    def toText: case self of
        POST:   "POST"
        GET:    "GET"
        PUT:    "PUT"
        DELETE: "DELETE"

class HttpResponse:
    HttpResponse Int Binary

    def body: case self of
        HttpResponse code body: body

    def text: self.body.toText

    def json: parseJSON self.body.toText

    def responseCode: case self of
        HttpResponse code body: code

class HttpRequest:
    HttpRequestClass
    HttpRequest Text HttpMethod (Map Text Text) (Maybe (Tuple2 Text Text)) Binary

    def default uri: HttpRequest "" GET Map.empty Nothing "".toBinary

    def method: case self of
        HttpRequest uri method headers auth body: method

    def setMethod m: case self of
        HttpRequest uri method headers auth body: HttpRequest uri m headers auth body

    def body: case self of
        HttpRequest uri method headers auth body: body

    def setBody b: case self of
        HttpRequest uri method headers auth body: HttpRequest uri method headers auth b

    def uri: case self of
        HttpRequest uri method headers auth body: uri

    def setUri u: case self of
        HttpRequest uri method headers auth body: HttpRequest u method headers auth body

    def headers: case self of
        HttpRequest uri method headers auth body: headers

    def addHeader key val: case self of
        HttpRequest uri method headers auth body: HttpRequest uri method (headers.insert key val) auth body

    def setAuth username password: case self of
        HttpRequest uri method headers auth body: HttpRequest uri method headers (Just (username, password)) body

    def perform: primPerformHttp self.uri (self.method.toText) (self.headers.toList) auth self.body

class Http:
    Http

    def get uri:
        HttpRequestClass.default uri

    def post uri body:
        HttpRequestClass.default uri . setMethod POST . setBody body

    def put uri body:
        HttpRequestClass.default uri . setMethod PUT . setBody body

    def delete uri:
        HttpRequestClass.default uri . setMethod DELETE

    def getBinary uri:
        Http.get uri . perform . body

    def getJSON uri:
        Http.get uri . perform . json
